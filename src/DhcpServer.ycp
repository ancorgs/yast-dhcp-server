/**
 * File:	modules/DhcpServer.ycp
 * Package:	Configuration of dhcp-server
 * Summary:	Data for configuration of dhcp-server,
 *              input and output functions.
 * Authors:	Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 *
 * Representation of the configuration of dhcp-server.
 * Input and output routines.
 */

{

    module "DhcpServer";
    textdomain "dhcp-server";

    import "Progress";
    import "Report";
    import "Summary";
    import "Runlevel";
    import "Popup";
    import "Require";
    import "Mode";

    include "dhcp-server/routines.ycp";
    include "dhcp-server/helps.ycp";
    include "dhcp-server/widgets.ycp";
    include "dhcp-server/options.ycp";

    /**
      * Abort function
      * return boolean return true if abort
      */
    global block AbortFunction = nil;

    /**
      * Data was modified?
      */
    global boolean modified = false;

    global boolean proposal_valid = false;

    /**
      * Write only, used during autoinstallation.
      * Don't run services and SuSEconfig, it's all done at one place.
      */
    global boolean write_only = false;

    global map settings = $[];

    global map widgets = $[];

    global map popups = $[];

    global map current_section = $[];

    global map current_list = $[];

    global string current_identifier = "";

    global boolean cfg_restart_wanted = false;

    /**
      * Constructor
      */
    global define void DhcpServer () ``{
	InitPopups ();
	InitWidgets ();
    }

    /**
      * Read all dhcp-server settings
      * @return true on success
      */
    global define boolean Read() ``{

	/* Dhcp-server read dialog caption */
	string caption = _("Initializing DHCP Server Configuration");

	Progress::New( caption, " ", 1, [
	    // progress stage
	    _("Read the settings"),
	],
	[
	    // progress step
	    _("Reading the settings..."),
	    // progress step
	    _("Finished")
	],
	getInitReadHelp ()
	);

	// read database
	if(UI::PollInput() == `abort) return false;
	Progress::NextStage();

	if (! Require::AreAllPackagesInstalled (["dhcp-server"]) || Mode::config)
	{
            boolean install_ok = Require::RequireAndConflictTarget (["dhcp-server"], [],
		// richtext, %1 is name of package
		_("For running DHCP sevrer, a DHCP daemon is required.
YaST2 will install package %1."));
	    if (! install_ok && ! Require::LastOperationCanceled ())
	    {
		// error popup
		Report::Error (_("Installing required packages failed."));
	    }
	}

	map bash_out = SCR::Execute (.target.bash_output,
"/usr/bin/grep '^# Creation time:' /etc/dhcpd.conf |head -n 1 |sed 's/# Creation time: \\([^$]*\\)/\\1/'");

	bash_out = SCR::Execute (.target.bash_output, sformat ("date --date='%1' +\"%%s\"", bash_out["stdout"]:""));

	map stat = SCR::Read (.target.stat, "/etc/dhcpd.conf");

	integer inode_time = stat["mtime"]:0;
	integer file_time = tointeger (bash_out["stdout"]:"0");

	integer difference = inode_time - file_time;
	y2milestone ("Difference between last modification time and value stored in file is %1 seconds", difference);

	if (0 < SCR::Read (.target.size, "/var/lib/YaST2/dhcp_server.ycp"))
	    settings = SCR::Read (.target.ycp, "/var/lib/YaST2/dhcp_server.ycp");
	else
	    settings = nil;

        if (settings == nil)
        {
	    // yes-no popup
	    // is not able means the feature is missing
	    if (! Popup::ContinueCancel (_("Warning!

You have not used YaST for configuring DHCP server yet.
Creating initial settings.  Add at least one subnet
to make the DHCP server work.

/etc/dhcpd.conf is rewritten when saving the
configuration.")))
		return false;
            settings = createSettings ();
        }

	else if (difference > 3)
	{
	    // yes-no popup
	    if (! Popup::YesNo (_("Warning!

/etc/dhcpd.conf was created or modified manually or using
a tool other than YaST2. YaST2 will rewrite the changes
when saving the configuration.

Continue?
")))
		return false;
	}

	if(UI::PollInput() == `abort) return false;
	modified = false;
	return true;
    }

    /**
      * Write all dhcp-server settings
      * @return true on success
      */
    global define boolean Write() ``{

	/* Dhcp-server read dialog caption */
	string caption = _("Saving DHCP Server Configuration");

	// We do not set help text here, because it was set outside
	Progress::New(caption, " ", 2, [
	    // progress stage
	    _("Write the settings"),
	    // progress stage
	    _("Restart DHCP server"),
	], [
	    // progress step
	    _("Writing the settings..."),
	    // progress step
	    _("Restarting DHCP server..."),
	    // progress step
	    _("Finished")
	],
	getWriteHelp ()
	);

	// write settings
	if(UI::PollInput() == `abort) return false;
	Progress::NextStage();

	createDhcpdConf ();
	SCR::Write (.target.ycp, "/var/lib/YaST2/dhcp_server.ycp", settings);

	if(UI::PollInput() == `abort) return false;
	Progress::NextStage();

	if (settings["__run_dhcp_server"]:false)
	{
	    integer ret
		= SCR::Execute (.target.bash, "/etc/init.d/dhcpd restart");
	    Runlevel::ServiceAdjust ("dhcpd", "enable");
	    if (0 != ret)
	    {
		// yes-no popup
		if (Popup::YesNo (_("Warning!

An error occurred while starting the DHCP server.
Display the log and continue with configuration?
")))
		{
		    DhcpServer::showLogPopup ("", $[]);
		    cfg_restart_wanted = true;
		}
		return false;
	    }
	}
	else
	{
	    SCR::Execute (.target.bash, "/etc/init.d/dhcpd stop");
	    Runlevel::ServiceAdjust ("dhcpd", "disable");
	}

	if(UI::PollInput() == `abort) return false;
	return true;
    }

    /**
      * Get all dhcp-server settings from the first parameter
      * (For use by autoinstallation.)
      * @param import_settings The YCP structure to be imported.
      * @return boolean True on success
      */
    global define boolean Import (map import_settings) ``{
	settings = import_settings;
	return true;
    }

    /**
      * Dump the dhcp-server settings to a single map
      * (For use by autoinstallation.)
      * @return map Dumped settings (later acceptable by Import ())
      */
    global define map Export () ``{
	return settings;
    }

    /**
      * Create a textual summary and a list of unconfigured cards
      * @return summary of the current configuration
      */
    global define list Summary() ``{
	string status = settings["__run_dhcp_server"]:false
		// summary text
	    ? _("The DHCP server is started at boot time")
		// summary text
	    : _("The DHCP server is not started at boot time");
	return [ status ];
    }

/* EOF */
}
