/**
 * File:	include/dhcp-server/widgets.ycp
 * Package:	Configuration of dhcp-server
 * Summary:	Widgets
 * Authors:	Jiri Srain <jsrain@suse.cz>
 *
 * $Id$
 */

{

    textdomain "dhcp-server";

    import "DhcpServer";
    import "CWM";
    import "TablePopup";

    import "Popup";
    import "Label";

// generic routines

    /**
      * Fallback function to initialize the settings in the popup
      * @param opt_id any option id
      * @param key any option key
      */
    global define void commonPopupInit (any opt_id, string key) ``{
	if (! is (opt_id, string))
	    return ;
	integer index = tointeger (
	    regexpsub ((string)opt_id, "^[a-z]+ ([0-9]+)$", "\\1"));
	if (substring (key, 0, 7) == "option ")
	{
	    UI::ChangeWidget (`id (key), `Value,
		current_entry_options[index, "value"]:"");
	}
	else
	{
	    UI::ChangeWidget (`id (key), `Value,
		current_entry_directives[index, "value"]:"");
	}
	UI::SetFocus (`id (key));
    }

    /**
      * Fallback function to save settings from the popup
      * @param opt_id any option id
      * @param key any option key
      * @return any modified operation if needed
      */
    global define void commonPopupSave (any opt_id, string key) ``{
	any value = UI::QueryWidget (`id (key), `Value);
	if (opt_id == nil)
	{
	    if (substring (key, 0, 7) == "option ")
	    {
		current_entry_options = add (current_entry_options, $[
		    "key" : substring (key, 7),
		    "value" : value,
		]);
	    }
	    else
	    {
		 current_entry_directives = add (current_entry_directives, $[
		    "key" : key,
		    "value" : value,
		]);
	    }
	    return;
	}
	if (! is (opt_id, string))
	    return ;
	integer index = tointeger (
	    regexpsub ((string)opt_id, "^[a-z]+ ([0-9]+)$", "\\1"));
	if (substring (key, 0, 7) == "option ")
	    current_entry_options[index, "value"] = value;
	else
	    current_entry_directives[index, "value"] = value;
	return;
    }

    /**
      * Fallback function to display summary text in the table
      * @param opt_id any option id
      * @param key any option key
      * @return string summary to be written to the table
      */
    global define string commonTableEntrySummary (any opt_id, string key) ``{
	if (! is (opt_id, string))
	    return "";
	integer index = tointeger (
	    regexpsub ((string)opt_id, "^[a-z]+ ([0-9]+)$", "\\1"));
	if (substring (key, 0, 7) == "option ")
	    return sformat ("%1", current_entry_options[index, "value"]:"");
	else
	    return sformat ("%1", current_entry_directives[index, "value"]:"");
    }

    /**
      * Function for deleting entry from section
      * Used for all (global, host, subnet) section due to the same location
      *  of data
      * @param opt_id any option id
      * @param key any option key
      * @return boolean true if was really deleted
      */
    global define boolean commonTableEntryDelete (any opt_id, string key) ``{
	if (! is (opt_id, string))
	    return false;
	integer index = tointeger (
	    regexpsub ((string)opt_id, "^[a-z]+ ([0-9]+)$", "\\1"));
	if (substring (key, 0, 7) == "option ")
	{
	    current_entry_options[index] = nil;
	    current_entry_options = filter (any o, current_entry_options,
	    ``( o != nil));
	}
	else
	{
	    current_entry_directives[index] = nil;
	    current_entry_directives = filter (any d, current_entry_directives,
	    ``( d != nil));
	}
	return true;
    }

    /**
      * Create list of identifiers of etries that should be present in the table
      * @param descr map description of the table
      * @return list of identifiers of entries of the table
      */
    global define list getTableContents (map descr) ``{
	integer index = -1;
	list<string> opts = maplist (map m, current_entry_options, ``{
	    index = index + 1;
	    return sformat ("option %1", index);
	});

	index = -1;
	list<string> dirs = maplist (map m, current_entry_directives, ``{
	    index = index + 1;
	    return sformat ("directive %1", index);
	});
	return merge (dirs, opts);
    }

    /**
      * Transform table entry id to option id
      * @param table map table description
      * @param id any entry id
      * @return any option key
      */
    global define string id2key (map table, any id) ``{
	if (! is (id, string))
	    return "";
	string strid = (string)id;
	if (substring (strid, 0, 7) == "option ")
	{
	    integer index = tointeger (substring (strid, 7));
	    return sformat ("option %1",
		current_entry_options[index, "key"]:"");
	}
	else if (substring (strid, 0, 10) == "directive ")
	{
	    integer index = tointeger (substring (strid, 10));
	    return current_entry_directives[index, "key"]:"";
	}
	return strid;
    }

    /**
      * Get the popup widget description map
      * @param opt_key any option key
      * @return map popup description map
      */
    global define map key2descr (any opt_key) ``{
	map ret = (map)(popups[opt_key]:nil);
	if (ret != nil)
	    return ret;
	return $[
                "init" : commonPopupInit,
                "store" : commonPopupSave,
	];
    }

    /**
      * Handle function of the log button
      * @param key any widget key
      * @param event map event that occured
      * @return value for wizard sequencer, always nil
      */
    global define any showLogPopup (any key, map event) ``{
/*	map bash_out = SCR::Execute (.target.bash_output,
	    "grep 'dhcpd' /var/log/messages | tail -n 50");
	UI::OpenDialog (`VBox (
	    // log view header
	    `HSpacing (70),
	    // log view header
	    `LogView (`id (`log), _("DHCP Server Log"), 20, 50),
	    `HBox (
		`HStretch (),
		`PushButton (`id (`close), Label::CloseButton ()),
		`HStretch ()
	    )));
	UI::ChangeWidget (`id (`log), `Value, bash_out["stdout"]:"");
	any ret = nil;
	while (ret != `close)
	    ret = UI::UserInput ();

	UI::CloseDialog ();*/
	return nil;
    }

    /**
      * Get map of widget
      * @param add_values list of values to be offered via the add button
      * @return map of widget
      */
    global define map getOptionsTableWidget (list add_values) ``{
	map ret = TablePopup::CreateTableDescr (
	    $["add_delete" : true, "up_down" : false],
	    $[
		"init": TablePopup::TableInitWrapper,
		"handle": TablePopup::TableHandleWrapper,
		"options": popups,
		"id2key": id2key,
		"ids": getTableContents,
		"help": getOptionsHelp (),
		"fallback" : $[
		    "init": commonPopupInit,
		    "store": commonPopupSave,
		    "summary": commonTableEntrySummary,
		],
		"option_delete": commonTableEntryDelete,
		"add_items":  add_values,
	    ]
	);
	return ret;
    }

    /**
      * Check whether settings were changed and if yes, ask for exit
      * without saving
      * @return event that should be handled, nil if user canceled the exit
      */
    global define boolean confirmAbort () ``{
        return Popup::YesNo (
// Yes-No popup
_("Really leave the DHCP server configuration without saving?
All changes will be lost."));
    }


global define symbol dhcpEnabledOrDisabled (string id, map event) ``{
    any ev_id = event["ID"]:nil;
    if (ev_id == "boot" || ev_id == "never")
    {
	boolean enabled = UI::QueryWidget (`id ("start"), `CurrentButton)
	    != "never";
	UI::ChangeWidget (`id (id), `Enabled, enabled);
    }
}

/**
 * Initialize the widget
 * @param id any widget id
 */
global define void startInit (string id) ``{
    boolean ss = DhcpServer::GetStartService();
    UI::ChangeWidget (`id ("start"), `Value, ss);
}

/**
 * Store settings of the widget
 * @param id any widget id
 * @param event map event that caused storing process
 */
global define void startStore (string id, map event) ``{
    DhcpServer::SetStartService (
	(boolean)UI::QueryWidget (`id ("start"), `Value));
}

/**
 * Handle function of the widget
 * @param id any widget id
 * @param event map event that caused storing process
 * @return any always nil
 */
global define any startHandle (string id, map event) ``{
    boolean start = (boolean)UI::QueryWidget (`id ("start"), `Value);
    if (start != DhcpServer::GetStartService ())
	DhcpServer::SetModified ();
    return nil;
}

global define void interfaceInit (string id) ``{
    list<string> ifaces = DhcpServer::GetAllowedInterfaces ();
    UI::ChangeWidget (`id (id), `Value, ifaces[0]:"");
}

global define void interfaceStore (string id, map event) ``{
    string iface = (string)UI::QueryWidget (`id (id), `Value);
    list<string> l = [ iface ];
    DhcpServer::SetAllowedInterfaces (l);
}

global define void rangeInit (string id) ``{
    return;
}

global define void rangeStore (string id, map event) ``{
    return;
}

global define void configTreeInit (string id);

global define symbol configTreeHandle (string id, map event) ``{
    string current_item = (string)
	UI::QueryWidget (`id ("configtree"), `CurrentItem);
    if (current_item == " ")
    {
	UI::ChangeWidget (`id (`delete), `Enabled, false);
	UI::ChangeWidget (`id (`move), `Enabled, false);
    }
    else
    {
	UI::ChangeWidget (`id (`delete), `Enabled, true);
	UI::ChangeWidget (`id (`move), `Enabled, true);
    }
    if (event["ID"]:nil == "configtree"
	&& event["EventReason"]:nil == "Activated")
    {
	event["ID"] = `edit;
    }

    map selected = key2typeid (current_item);
    if (selected == nil)
    {
	y2error ("Unexistent entry selected");
	return nil;
    }
    if (event["ID"]:nil == `add)
    {
	parent_type = selected["type"]:"";
	parent_id = selected["id"]:"";
	current_entry_options = [];
	current_entry_directives = [];
	current_entry_type = "";
	current_entry_id = "";
	return `add;
    }
    else if (event["ID"]:nil == `edit)
    {
	current_entry_type = selected["type"]:"";
	current_entry_id = selected["id"]:"";
	current_entry_options = DhcpServer::GetEntryOptions (
	    current_entry_type,
	    current_entry_id);
	current_entry_directives = DhcpServer::GetEntryDirectives (
	    current_entry_type,
	    current_entry_id);

	original_entry_type = current_entry_type;
	original_entry_id = current_entry_id;
	parent_type = "";
	parent_id = "";

	return `edit;
    }
    else if (event["ID"]:nil == `delete)
    {
	DhcpServer::DeleteEntry (selected["type"]:"", selected["id"]:"");
	configTreeInit (id);
    }
    else if (event["ID"]:nil == `move)
    {

// TODO
    }
    return nil;
}

global define void configTreeInit (string id) ``{
    list items = getItems ("", "");
    items = [
	`item (`id (" "), _("Global options"), true, items)
    ];
    UI::ReplaceWidget (`configtree_rp, `Tree (
	`id ("configtree"),
	`opt (`notify),
	_("&Configuration"),
        items
    ));
    UI::ChangeWidget (`id ("configtree"), `CurrentItem, " ");
    configTreeHandle (id, $[]);
    return;
}

global define void subnetInit (string id) ``{
    list<string> l = regexptokenize (current_entry_id,
	"^[ \t]*([^ \t]+)[ \t]*netmask[ \t]*([^ \t]+)[ \t]*$");
    UI::ChangeWidget (`id (`subnet), `Value, l[0]:"");
    UI::ChangeWidget (`id (`netmask), `Value, l[1]:"");
}

global define void subnetStore (string id, map event) ``{
    string id = sformat ("%1 netmask %2",
	(string)UI::QueryWidget (`id (`subnet), `Value),
	(string)UI::QueryWidget (`id (`netmask), `Value));
    current_entry_id = id;
}



    /**
      * Initialize popups
      * Create description map and copy it into appropriate variable of the
      *  DhcpServer module
      */
    global define void InitWidgets () ``{
	list options = [
"option subnet-mask",
"option broadcast-address",
"option routers",
"option static-routes",
"option domain-name",
"option domain-name-servers",
"option host-name",
"option root-path",
"option tftp-server-name",
"option bootfile-name",
"option dhcp-server-identifier",
"option time-servers",
"option ntp-servers",
"option log-servers",
"option lpr-servers",
"option font-servers",
"option x-display-managers",
"option smtp-server",
"option pop-server",
"option irc-server",
"option nis-domain",
"option nis-servers",
"option nisplus-domain",
"option nisplus-servers",
"option interface-mtu",
"option vendor-encapsulated-options",
"option vendor-class-identifier",
"option netbios-name-servers",
"option netbios-dd-server",
"option netbios-note-type",
"option netbios-scope",
	];

	list common_commands = [
"max-lease-time",
"default-lease-time",
"filename",
"next-server",
	];

	list global_commands = [
"authoritative",
"ddns-update-style none",
"ddns-updates off",
"log-facility",
	];

	list subnet_commands = [
"range",
	];

	list host_commands = [
"hardware",
"fixed-address",
	];

	common_commands = merge (common_commands, options);
	global_commands = toset (merge (global_commands, common_commands));
	subnet_commands = toset (merge (subnet_commands, common_commands));
	host_commands = toset (merge (host_commands, common_commands));

	map w = $[
	    "global_table" : getOptionsTableWidget (global_commands),
	    "host_table" : getOptionsTableWidget (subnet_commands),
	    "subnet_table" : getOptionsTableWidget (host_commands),
	    "shared-network_table" : getOptionsTableWidget (common_commands),
	    "pool_table" : getOptionsTableWidget (common_commands),
	    "group_table" : getOptionsTableWidget (common_commands),
	    "log_button" : $[
		"widget" : `push_button,
		// push button
		"label" : _("&Display Log"),
		"handle" : showLogPopup,
		"handle_events" : [ "log_button" ],
		"help" : getLogButtonHelp (),
	    ],
	    "start" : $[
		"widget" : `checkbox,
		"label" : _("&Start DHCP server"),
		"help" : HELPS["start"]:"TODO",
		"init" : startInit,
		"handle" : startHandle,
		"store" : startStore,
		"opt" : [`notify],
	    ],
	    "interface" : $[
		"widget" : `textentry,
		"label" : _("Network &interface"),
		"help" : HELPS["interface"]:"FIXME",
		"init" : interfaceInit,
		"store" : interfaceStore,
	    ],
	    "configtree" : $[
		"widget" : `custom,
		"custom_widget" : `VBox (
		    `ReplacePoint (`id (`configtree_rp), `Tree (
			`id ("configtree"),
			_("&Configuration"),
			[]
		    )),
		    `HBox (
			`PushButton (`id (`add), Label::AddButton ()),
			`PushButton (`id (`edit), Label::EditButton ()),
			`PushButton (`id (`delete), Label::DeleteButton ()),
			`PushButton (`id (`move), _("&Move")),
			`HStretch (),
			`MenuButton (`id (`adv), _("Ad&vanced"), [])
		    )),
		"help" : HELPS["configtree"]:"TODO",
		"init" : configTreeInit,
		"handle" : configTreeHandle,
	    ],
	    "subnet" : $[
		"widget" : `custom,
		"custom_widget" : `HBox (
		    `HSpacing (2),
		    `TextEntry (`id (`subnet), _("Subnet")),
		    `TextEntry (`id (`netmask), _("Network mask")),
		    `HSpacing (2)
		),
		"help" : HELPS["subnet"]:"TODO",
		"init" : subnetInit,
		"store" : subnetStore,
	    ],
	];
	DhcpServerUI::widgets = w;
    }


}
